       IDENTIFICATION DIVISION.
      ******************************************************************
      *   SOUS-PROGRAMME DE LECTURE D'UN ASSURE                        *
      ******************************************************************
       PROGRAM-ID. SPROG61.

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.

      ******************************************************************
      *   DECLARATION DU FICHIER ASSURES KSDS                          *
      ******************************************************************
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
            SELECT ASSURES ASSIGN TO FASSURES
                   ORGANIZATION   IS INDEXED
                   ACCESS MODE    IS RANDOM
                   RECORD KEY     IS ENRB-NUMERO
                   FILE STATUS    IS FS-ASSURES.

       DATA DIVISION.
      ******************************************************************
      *   DESCRIPTION DES BUFFERS                                      *
      ******************************************************************
       FILE SECTION.
       FD  ASSURES.
           COPY ASSURES2 REPLACING ==:PREF:== BY ==ENRB==.

       WORKING-STORAGE SECTION.
      ******************************************************************
      *   DESCRIPTION DES ENREGISTREMENTS EN WORKING                   *
      ******************************************************************
           COPY ASSURES2 REPLACING ==:PREF:== BY ==ENRW==.

      ******************************************************************
      *   DECLARATION DU FILE STATUS                                   *
      ******************************************************************
       01  FS-ASSURES          PIC 99         VALUE ZEROES.

       LINKAGE SECTION.
      ******************************************************************
      *   DESCRIPTION DE LA ZONE DE COMMUNICATION AVEC LE PROG APPELANT*
      ******************************************************************
       01  ZCOM-LEC.
           05 ZCOM-NUMERO      PIC X(6).
           05 ZCOM-ASSURES     PIC X(80).
           05 ZCOM-CODE-RETOUR PIC 9.
              88 ZCOM-TROUVE     VALUE 0.
              88 ZCOM-NON-TROUVE VALUE 1.
              88 ZCOM-ERREUR     VALUE 9.

       PROCEDURE DIVISION USING ZCOM-LEC.
           PERFORM DEBUT
           PERFORM TRAITEMENT
           PERFORM FIN
           .

       DEBUT.
      * OUVERTURE DU FICHIER ASSURES
           OPEN INPUT ASSURES
           IF FS-ASSURES NOT = ZEROES
              DISPLAY 'ERREUR OUVERTURE FICHIER ASSURES, FS : '
                      FS-ASSURES
              PERFORM FIN
           END-IF
           .

       TRAITEMENT.
      * ON RENSEIGNE LA CLE DU FICHIER
           MOVE ZCOM-NUMERO TO ENRB-NUMERO

      * LECTURE ASSURE AVEC LA CLE
           READ ASSURES INTO ENRW-ASSURES
             INVALID KEY
              SET ZCOM-NON-TROUVE TO TRUE
             NOT INVALID KEY
              SET ZCOM-TROUVE TO TRUE
              MOVE ENRW-ASSURES TO ZCOM-ASSURES
           END-READ
           IF FS-ASSURES NOT = ZEROES AND 23
              SET ZCOM-ERREUR TO TRUE
              PERFORM FIN
           END-IF
           .

       FIN.
      * FERMETURE DES FICHIERS
           CLOSE ASSURES
           GOBACK
           .
